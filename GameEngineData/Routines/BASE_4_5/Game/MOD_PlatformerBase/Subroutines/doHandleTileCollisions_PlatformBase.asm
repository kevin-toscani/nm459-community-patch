


MACRO CheckCollisionAtTileXY
    JSR GetTileAtPosition
    LDA tempx
    BEQ +evenCollisionTable

    +oddCollisionTable:
    LDA collisionTable2,y
    JMP +checkPoint

    +evenCollisionTable:
    LDA collisionTable,y

    +checkPoint:
    BEQ +checkNextPoint
    CMP #$01
    BNE +checkIfFirstNonZeroCollision
        ;STA tempA
        LDA tempx
        STA temp2
        STY tempy
        LDA #$01
        RTS

    +checkIfFirstNonZeroCollision:
    STA temp
    LDA tempy
    BNE +checkNextPoint
        LDA tempx
        STA temp2
        LDA temp
        STA tempA
        STY tempy
    +checkNextPoint:
ENDM



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; doHandleTileCollisionsLikeANormalPerson.asm
;;


    ;; Reset the end result variable
    LDA #$00
    STA tempA
    STA tempy

    ;; Store if current screen is odd or even in a temp variable
    LDA xHold_screen
    AND #%00000001
    STA tempx

    ;; Check point 1 (the top right of the object)
    LDA yHold_hi
    CLC
    ADC self_top
    STA tileY

    LDA xHold_hi
    CLC
    ADC self_right
    STA tileX
    BCC +getTileOffset
        ;; We've crossed a page boundary; flip the screen number
        LDA tempx
        EOR #%00000001
        STA tempx
    +getTileOffset:
    CheckCollisionAtTileXY

    ;; Check point 2 (the bottom right of the object)
    LDA yHold_hi
    CLC
    ADC self_bottom
    STA tileY
    CheckCollisionAtTileXY

    ;; Reset xHold_screen for left boundary check
    LDA xHold_screen
    AND #%00000001
    STA tempx

    ;; Check point 3 (the bottom left of the object)
    LDA xHold_hi
    CLC
    ADC self_left
    STA tileX
    BCC +getTileOffset
        ;; We've crossed a page boundary; flip the screen number
        LDA tempx
        EOR #%00000001
        STA tempx
    +getTileOffset:
    CheckCollisionAtTileXY

    ;; Check point 4 (the top left of the object)
    LDA yHold_hi
    CLC
    ADC self_top
    STA tileY
    CheckCollisionAtTileXY

    ;; All checked: load tile offset and type and return
    LDY tempy
    LDA tempA
    ;RTS

